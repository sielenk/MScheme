apply plugin: 'java'

String mainClass = 'mscheme.Main'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Main-Class': mainClass
    }
}


task execute(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClass

    enableAssertions = true
}

task createInit {
    def inputFiles = fileTree('src/main/scheme') {
        include '**/*.scm'
    }
    def outputDir = file("$buildDir/generated-src")

    inputs.files inputFiles
    outputs.dir outputDir

    doLast {
        def body =
                inputFiles.collect {
                    def name = (it.name - '.scm').toUpperCase()
                    def value = it.text
                            .replaceAll(/;.*/, '')
                            .replaceAll(/(["\\])/, /\\$1/)
                            .replace('\n', ' ')
                            .replaceAll(/\s+/, ' ')
                            .replaceAll(/ ?(\(|\)|\[|\]) ?/, /$1/)

                    "    String $name = \"$value\";"
                }

        def fileContent = [
                'package mscheme;',
                '',
                'public interface IInit',
                '{',
                body,
                '}'
        ].flatten()

        new File(outputDir, 'IInit.java')
                .write(fileContent.join('\n'))
    }
}

compileJava {
    dependsOn createInit
    source createInit.outputs.files
}
